plugins {
    id 'io.github.h4j4x.codegen.java-common-conventions'
    id 'maven-publish'
    id 'signing'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Codegen'
                description = 'Generate code'
                url = 'https://github.com/h4j4x/codegen'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/h4j4x/codegen/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'h4j4x'
                        name = 'Michel Atucha'
                        email = 'miche.atucha@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/h4j4x/codegen.git'
                    developerConnection = 'scm:git:ssh://github.com/h4j4x/codegen.git'
                    url = 'https://github.com/h4j4x/codegen'
                }
            }
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
