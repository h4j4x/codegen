plugins {
    id 'io.github.ajax.codegen.java-application-conventions'
    id 'maven-publish'
    id 'signing'
}

version = '0.0.2'

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation project(':lib')
    implementation 'args4j:args4j:2.33'
}

application {
    mainClass = 'io.github.codegen.app.CliApp'
}

distributions {
    main {
        distributionBaseName = 'codegen-cli'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'codegen-cli'
            from components.java
            pom {
                name = 'Codegen'
                description = 'Generate code'
                url = 'https://github.com/miche-atucha/codegen'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/miche-atucha/codegen/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'miche-atucha'
                        name = 'Michel Atucha'
                        email = 'miche.atucha@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/miche-atucha/codegen.git'
                    developerConnection = 'scm:git:ssh://github.com/miche-atucha/codegen.git'
                    url = 'https://github.com/miche-atucha/codegen'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
